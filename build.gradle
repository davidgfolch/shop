plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "io.freefair.lombok" version "5.0.0-rc2"
    id "org.sonarqube" version "2.8"
    id "jacoco"
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
    }
}

allprojects {

}

subprojects {
    group = 'org.dgf.shop'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'jacoco'

//    jacoco {
//        toolVersion = '0.7.1.201405082137'
//    }

    repositories {
        jcenter()
        mavenCentral()
        maven {
            //For now, we plan to start releasing to Maven Central soon
            url "https://dl.bintray.com/mockito/maven"
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE")
        }
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    jacocoTestReport {
        getAdditionalSourceDirs().setFrom(files(sourceSets.main.allSource.srcDirs))
        getSourceDirectories().setFrom(files(sourceSets.main.allSource.srcDirs))
        getClassDirectories().setFrom(files(sourceSets.main.output))
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    getAdditionalSourceDirs().setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    getSourceDirectories().setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    getClassDirectories().setFrom(files(subprojects.sourceSets.main.output))
    getExecutionData().setFrom(files(subprojects.jacocoTestReport.executionData))
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

